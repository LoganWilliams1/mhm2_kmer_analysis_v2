if(NOT ENABLE_CUDA)
    message(FATAL_ERROR "Trying to build ADEPT-SW but CUDA is not enabled")
endif()


set(CMAKE_CUDA_STANDARD 14 CACHE STRING "")
set(CMAKE_CUDA_STANDARD_REQUIRED ON CACHE BOOL "")
set(CMAKE_CUDA_EXTENSIONS OFF CACHE BOOL "")

find_package(OpenMP REQUIRED)

if(CMAKE_VERSION VERSION_GREATER_EQUAL 3.12)
  cmake_policy(SET CMP0074 NEW) # use the CUDA_ROOT variable 
endif()

enable_language(CUDA)
if (${CMAKE_VERSION} VERSION_GREATER_EQUAL 3.18)
  if(NOT CMAKE_CUDA_ARCHITECTURES)

    if (CUDA_VERSION VERSION_GREATER_EQUAL 11.0)
      set(CMAKE_CUDA_ARCHITECTURES 35 50 60 70 80)
    elseif (CUDA_VERSION VERSION_GREATER_EQUAL 10.0)
      set(CMAKE_CUDA_ARCHITECTURES 30 35 50 60 70)
    else()
      set(CMAKE_CUDA_ARCHITECTURES 30 35 50 60 70)
    endif()
    
    message(STATUS "Setting CMAKE_CUDA_ARCHITECTURES=${CMAKE_CUDA_ARCHITECTURES} -- override with specific target if this build fails")
  else()
    message(STATUS "Using specified CMAKE_CUDA_ARCHITECTURES=${CMAKE_CUDA_ARCHITECTURES}")
  endif()
endif()

set(CMAKE_POSITION_INDEPENDENT_CODE ON)
set(CMAKE_CUDA_SEPARABLE_COMPILATION ON)
set(CMAKE_CUDA_PTX_COMPILATION ON)

if (${CMAKE_VERSION} VERSION_LESS 3.18)
  find_package(CUDA REQUIRED)
  CUDA_SELECT_NVCC_ARCH_FLAGS(ARCH_FLAGS_LIST)
  if(ARCH_FLAGS_LIST)
    STRING(REPLACE ";" " " ARCH_FLAGS "${ARCH_FLAGS_LIST}")
  else()
    foreach(_CMAKE_CUDA_COMPUTE_CAPABILITY 35 50 60 70 80)
      set(ARCH_FLAGS "${ARCH_FLAGS} -gencode arch=compute_${_CMAKE_CUDA_COMPUTE_CAPABILITY},code=sm_${_CMAKE_CUDA_COMPUTE_CAPABILITY}")
    endforeach()
    if(NOT CMAKE_CUDA_COMPUTE_CAPABILITY)
      set(CMAKE_CUDA_COMPUTE_CAPABILITY 70 CACHE STRING "")
    endif()
    message(WARNING "Could not auto-detect the GPU arch flags building for all gpu compute capabilities 3 - 7 and PTX for ${CMAKE_CUDA_COMPUTE_CAPABILITY}")
    set(ARCH_FLAGS "${ARCH_FLAGS} -gencode arch=compute_${CMAKE_CUDA_COMPUTE_CAPABILITY},code=sm_${CMAKE_CUDA_COMPUTE_CAPABILITY}")
    set(ARCH_FLAGS "${ARCH_FLAGS} -gencode arch=compute_${CMAKE_CUDA_COMPUTE_CAPABILITY},code=compute_${CMAKE_CUDA_COMPUTE_CAPABILITY}")
  endif()
endif()

message(STATUS "ARCH_FLAGS=${ARCH_FLAGS}")


set(CMAKE_CUDA_FLAGS "${CMAKE_CUDA_FLAGS} -Xcompiler=-Wall,${OpenMP_CXX_FLAGS} ${ARCH_FLAGS}")
message(STATUS "CMAKE_CUDA_FLAGS=${CMAKE_CUDA_FLAGS}")

add_library(MHM2_GPU_LIBRARY_obj OBJECT dummy.cpp)
if (${CMAKE_VERSION} VERSION_GREATER_EQUAL 3.13)
  target_link_libraries(MHM2_GPU_LIBRARY_obj INTERFACE OpenMP::OpenMP_CXX)
endif()

option(MHM2_GPU_SHARED "MHM2-GPU shared library" OFF)
option(MHM2_GPU_STATIC "MHM2-GPU static library" ON)

if (MHM2_GPU_SHARED)
  add_library(MHM2_GPU_LIBRARY_shared SHARED $<TARGET_OBJECTS:ADEPT_SW_LIBRARY_obj> $<TARGET_OBJECTS:KCOUNT_GPU_LIBRARY_obj> $<TARGET_OBJECTS:GPU_UTILS_LIBRARY_obj>)
  set_property(TARGET MHM2_GPU_LIBRARY_shared PROPERTY CUDA_RESOLVE_DEVICE_SYMBOLS ON)
  target_link_libraries(MHM2_GPU_LIBRARY_shared INTERFACE OpenMP::OpenMP_CXX) 
  install(TARGETS MHM2_GPU_LIBRARY_shared LIBRARY DESTINATION lib)
endif()
if (MHM2_GPU_STATIC)
  add_library(MHM2_GPU_LIBRARY_static STATIC $<TARGET_OBJECTS:ADEPT_SW_LIBRARY_obj> $<TARGET_OBJECTS:KCOUNT_GPU_LIBRARY_obj> $<TARGET_OBJECTS:GPU_UTILS_LIBRARY_obj>)
  target_link_libraries(MHM2_GPU_LIBRARY_static INTERFACE OpenMP::OpenMP_CXX)
  set_property(TARGET MHM2_GPU_LIBRARY_static PROPERTY CUDA_RESOLVE_DEVICE_SYMBOLS ON)
  install(TARGETS MHM2_GPU_LIBRARY_static ARCHIVE DESTINATION lib)
endif()

